{
id: "pivot_properties_sword",
prototype: ["pivot_properties_anim"],

properties: {
	
	///////////////
	// CONSTANTS //
	///////////////	
	cIN_LAST_SWORD_ATTACK: "(
		sword + (cANIM_NUM + 1) not in available_animations
	)",
		
	////////////////////
	// VOID CONSTANTS //
	////////////////////
	vREQUEST_SWORD_ATTACK: "
		if(cANIM_GROUP = sword and cIN_END_ANIM and not cIN_LAST_SWORD_ATTACK,
			set(animation, NextAnimationInChain(sword, cANIM_NUM)),
		   cIN_CONTROLLABLE_ANIM,
			[set(animation, NextAnimationInChain(sword, 0)),
			 add(major_attack_num, 1)
			]
		)
	",

}, // End of properties

on_ctrl_sword: "[
	set(cycleOfLastSwordInput, cycle),
	vREQUEST_SWORD_ATTACK
]",

on_end_sword_anim: "[
	// If the player has given a recent enough attack input, continue the
	// chain. The idea is that button mashing WILL let you slash slightly
	// faster by skipping the first end animation frame.
if( not cIN_LAST_SWORD_ATTACK and tRECENT_SWORD_REQUEST,
		set(animation, NextAnimationInChain(sword, cANIM_NUM)),
	
	// Attempt to transition into an end animation.
	animation + 'E' in available_animations,
		animation(animation + 'E'),
		
	// If we're in a start animation, attempt to switch into the slash that
	// comes right afterward.
	cANIM_TYPE = 'Start' and
	 (cANIM_GROUP + FilterNull(cANIM_NUM)) in available_animations,
		animation(cANIM_GROUP + FilterNull(cANIM_NUM)),
	
	// If the player is done attacking, start standing.
	animation('stand')
	
	) where tRECENT_SWORD_REQUEST =
				me.cycle - cycleOfLastSwordInput < cSOONEST_SWORD_REQUEST,
]",

} // End of file
