{
id: "pivot_properties_sword",
prototype: ["pivot_properties_gun"],

properties: {
	
	///////////////////////////
	// CONSTANTS & VARIABLES //
	///////////////////////////

	// This is the currently equiped sword.
	sword: { type: "string", default: 'calibur' },
	
	// For each int list, there must be a value for EACH attack in the sword's attack set
	cSWORD_DATA: "{string ->
		{
			normal_damage:    [int],
			pierce_damage:    [int],
			elemental_damage: [int],
			shadow_damage:    [int],
			sound_effect_dir: string,
			charge_time:      int
		}
	} <- get_document('data/swords.json')",
	
	cIN_LAST_SWORD_ATTACK: "bool :: bool(
		sword + (cANIM_NUM + 1) not in available_animations
	)",
	
	// The way the system works is to allow the player to launch a second
	// sword attack if they press the button ANY TIME during the first one.
	cycleOfLastSwordInput: { type: "int", default: -1 },
	cycleOfSwordAnimBegin: { type: "int", default: -1 },
		
	///////////////
	// FUNCTIONS //
	///////////////
	RequestSwordAttack: "def() -> commands
		if(cANIM_NAME = sword and cIN_END_ANIM and not cIN_LAST_SWORD_ATTACK,
			LaunchNextSwordAttack(),
		   cIN_CONTROLLABLE_ANIM,
			[set(animation, NextAnimationInChain(sword, 0)),
			 set(cycleOfSwordAnimBegin, me.cycle),
			 add(major_attack_num, 1)
			]
		)
	",

	RequestSwordChargeAttack: "def() -> commands
		if(cIN_CONTROLLABLE_ANIM, [
			set(animation, sword + 'CS'),
			add(major_attack_num, 1)
		])
	",

	// This is called when we are mid-combo. This leads into next attack
	// and makes sure the variable for the start-of-sword animation is set.
	LaunchNextSwordAttack: "def() -> commands [
		set(animation, NextAnimationInChain(sword, cANIM_NUM)),
		set(cycleOfSwordAnimBegin, me.cycle)
	]",

}, // End of properties

on_ctrl_sword: "[
	set(cycleOfLastSwordInput, cycle),
	RequestSwordAttack()
]",

on_ctrl_sword_held: "[
	// Begin holding the charge ONLY when we are not in the middle of attacking
	if( cANIM_NAME != sword,	// Only charge while we aren't doing a sword attack
		if( not Timer_IsStarted('swordCharge'),
			Timer_Start('swordCharge', int <- cSWORD_DATA[sword]['charge_time'])
		)
	),
	
	if( Timer_IsStarted('swordCharge'),
		debug(Timer_GetRemainingTime('swordCharge'))
	)
]",

on_ctrl_sword_released: "
	// Only do anything if we have actually gathered any charge
	if( Timer_IsStarted('swordCharge'), [
	
		// If the timer's expired, then the charge is complete
		if( Timer_HasExpired('swordCharge'),
			RequestSwordChargeAttack(),
			RequestSwordAttack()
		),
		
		// We want to stop charging, no matter how charged the player became
		Timer_Stop('swordCharge')
	])
",

on_end_sword_anim: "[
	// If the player has given a recent enough attack input, continue the
	// chain. The idea is that button mashing WILL let you slash slightly
	// faster by skipping the first end animation frame.
if( not cIN_LAST_SWORD_ATTACK and tRECENT_SWORD_REQUEST,
	LaunchNextSwordAttack(),
	
	// Attempt to transition into an end animation.
	animation + 'E' in available_animations,
		animation(animation + 'E'),
		
	// If we're in a start animation, attempt to switch into the slash that
	// comes right afterward.
	cANIM_TYPE = 'Start' and
	 (cANIM_NAME + cANIM_NUM_TO_STR) in available_animations,
		animation(cANIM_NAME + cANIM_NUM_TO_STR),
			
	
	// If the player is done attacking, start standing.
	animation('stand')
	
	) // End if(not cIN_LAST_SWORD_ATTACK and tRECENT_SWORD_REQUEST, ...)
		where tRECENT_SWORD_REQUEST = cycleOfLastSwordInput > cycleOfSwordAnimBegin
]",

} // End of file
