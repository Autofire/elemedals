#Originates from Frogatto

{
id: "shot",
prototype: ["hittable"],

collide_dimensions: ["player","enemy","~hazard"],
solid_dimensions: ["enemy","player","common"],

#make it so when the object runs into solid parts of the level, an on_collide_level event is fired so the shot can be destroyed.
object_level_collisions: true,
has_feet: false,

/*
expected things a user of this prototype must implement to deal damage
	properties.attack_damage - how much damage is dealt
	properties.attack_knockback - a power-level for how hard it hits.
	properties.damage_cooldown - how long the enemy will be invincible, used to cap the DPS of flamethrower-esque weapons, whilst allowing nigh-unlimited numbers of shots to make the attack easy to hit with.
		Note that if cooldown is zero, the enemy should always take damage.
	animation attack_area - an area that will trigger a hit
	animation body_area - only set this if you want this shot to be neutralizable by enemy shots
*/
properties: {
	team: "string :: 'evil'",
	attack_knockback: "int :: 0",
	attack_damage: "int :: if(higher_difficulty, 2, 1)",
	damage_cooldown: "int :: 0",

		#only for death upon striking an enemy body/armor area, NOT for from hitting enemy attack areas.  Always dies instantly.
	dies_upon_dealing_damage: "bool :: true",

		#causes the shot to bleed HP (usually to die instantly because it has only one HP) if it hits an enemy attack area.  Primarily used to allow frogatto to breathe fire/energy-shots at the enemy and deflect their shots with his shots - almost always set in favor of the player.
	deflectable_via_attacks: "bool :: if(team = 'player', false, true)",  

		#these two properties exist because there may be cases (like the homing shot) where we want a projectile that does expire upon successfully hitting, but only if it successfully hits an actual enemy, not shots fired by the enemy.  
	is_a_shot: 	"bool :: true",
	goes_through_enemy_shots: "bool :: false",


	get_hit_by: "def(obj hittable collide_with) -> commands if( (collide_with.damage_cooldown < (level.cycle - time_last_hit)),
										[add(me.hitpoints, - collide_with.attack_damage), set(time_last_hit, level.cycle)])",

},
on_collide_side: "die()",
on_collide_level: "die()",


#--------------------------  collision handlers --------------------------#	
	#remember, these events are NOT for dealing damage to an enemy - the enemy takes care of that in its copies of these events (in hittable).  These are only for potentially dealing damage to ourselves.  The design pattern in frogatto is that the victim decides everything WRT its own damage; the attacker simply provides info the victim makes decisions with.

#if we hit our body, try bleeding HP from potential deflections
on_collide_object_body: "if(arg.collide_with is obj hittable, if(arg.collide_with.team != team and (arg.collide_with_area = 'attack' or arg.collide_with_area = 'thrown'),
								if(deflectable_via_attacks, get_hit_by(arg.collide_with))	))",

#since our attack hit, decide if we want to kill ourselves.		  
on_collide_object_attack: "if(arg.collide_with is obj hittable, if(arg.collide_with.team != team and arg.collide_with_area = 'body', 
									if(dies_upon_dealing_damage and if(arg.collide_with is obj shot, arg.collide_with.deflectable_via_attacks = true, true), if(me.goes_through_enemy_shots and arg.collide_with.is_a_shot, null, die()))	))",
}
