{
id: "periodic_spawning_enemy_generator",
always_active: true,
is_strict: true,
hidden_in_game: true,
prototype: ["standard_values"],
properties: {
	screen_left_side: "level.camera_position[0]-100",
	screen_right_side: "level.camera_position[0] + level.camera_position[2]+100",
	screen_top:	"level.camera_position[1]",
	screen_bottom: "level.camera_position[1]+level.camera_position[3]",
	screen_mid_y: "level.camera_position[1]+level.camera_position[3]/2",
	
	player_behind: "if(level.player.facing = 1, screen_left_side, screen_right_side)",
	random_side: "if(1d2=2, screen_left_side, screen_right_side)",
	
	player_within_bounds: "bool :: (level.player.mid_x > _x_bound and level.player.mid_x < _x2_bound and level.player.mid_y > _y_bound and level.player.mid_y < _y2_bound)",
	
	delay_between_spawns:  "if(higher_difficulty, _delay/3, _delay)", 
#-------------------------- vars --------------------------#
	_spawn_type: { type: "string", default: "finch", editor_info: { type: "enum", enum_values: ["finch"] } },  #type must be a child of 'ant_flying'
	_delay: { type: "int", default: 100, editor_info: { type: "int", value: "100" } },

	_x_bound: { type: "int", editor_info: { type: "x", value: "x-400" } },
	_x2_bound: { type: "int", editor_info: { type: "x", value: "x+400" } },
	_y_bound: { type: "int", editor_info: { type: "y", value: "y-400" } },
	_y2_bound: { type: "int", editor_info: { type: "y", value: "y+400" } },
},


on_load: "schedule(delay_between_spawns,fire_event('spawn_thing'))",

on_spawn_thing:	"[	if(_spawn_type and player_within_bounds,
						spawn(_spawn_type, random_side, screen_mid_y, level.player.facing, 
							[set((obj ant_flying <- child)._desired_height, level.player.mid_y), set((obj ant_flying <- child)._x_bound, _x_bound), set((obj ant_flying <- child)._x2_bound, _x2_bound)] )
					),
					schedule(delay_between_spawns,fire_event('spawn_thing'))
				]",


zorder: "@include data/zorder.cfg:in_front_of_everything",
editor_info: {
	category: "enemies, general",
},
animation: {
	id: "normal",
	image: "effects/particles.png",
	x: 148,
	y: 228,
	w: 28,
	h: 28,
	frames: 1,
	duration: 1000,
},
}